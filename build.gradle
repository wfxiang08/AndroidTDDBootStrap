// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'
apply from: 'buildsystem/variants.gradle'

buildscript {
    repositories {
        jcenter()
        maven {
            // For fabric
            url 'https://maven.fabric.io/public'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.1'
        classpath 'io.fabric.tools:gradle:1.21.4'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.3.0'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.3.6'
        classpath 'com.ofg:uptodate-gradle-plugin:1.6.2'
        classpath "com.github.piasy:okbuck-gradle-plugin:1.0.0-beta8"
        classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.5.0"
        classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            // For icepick
            url 'https://clojars.org/repo/'
        }
        maven {
            // For fabric
            url 'https://maven.fabric.io/public'
        }
    }
}

apply from: 'AndroidCodeQualityConfig/jacoco.gradle'

apply plugin: 'com.github.piasy.okbuck-gradle-plugin'
okbuck {
    target "android-23"
    overwrite true
    resPackages = [
            'base'    : 'com.github.piasy.base',
            'model'   : 'com.github.piasy.model',
            'app'     : 'com.github.piasy.app',
            'testbase': 'com.github.piasy.base.test',
    ]
    linearAllocHardLimit = [
            app: 7194304
    ]
    primaryDexPatterns = [
            app: [
                    '^com/github/piasy/app/BootstrapApp^',
            ]
    ]
}

apply plugin: "build-time-tracker"
buildtimetracker {
    reporters {
        csv {
            output "build/times.csv"
            append true
            header false
        }

        summary {
            ordered false
            threshold 50
            barstyle "unicode"
        }

        csvSummary {
            csv "build/times.csv"
        }
    }
}

